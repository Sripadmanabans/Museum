import org.jetbrains.kotlin.gradle.targets.js.webpack.KotlinWebpackConfig

plugins {
  id 'com.adjectivemonk2.kotlin.multiplatform'
  id 'org.jetbrains.kotlin.plugin.serialization'
  id 'app.cash.zipline'
}

kotlin {
  jvm {
    jvmToolchain(17)
  }

  js(IR) {
    // The name of the JS module which needs to be unique within the repo.
    moduleName = 'museum-presenter-treehouse-gallery'
    browser {
      commonWebpackConfig {
        mode = KotlinWebpackConfig.Mode.DEVELOPMENT
      }
    }
    binaries.executable()
  }

  sourceSets {
    commonMain {
      dependencies {
        api projects.remote.core.public
        api projects.remote.gallery.impl
        api projects.repository.gallery.impl

        api libs.redwood.treehouse
      }
    }

    jsMain {
      dependencies {
        api libs.redwood.treehouse.guest
        api projects.presenter.gallery

        implementation projects.schema.compose.protocol
      }
    }
  }
}

zipline {
  mainFunction = "com.adjectivemonk2.museum.presenter.treehouse.gallery.preparePresenters"
}
